apply plugin: 'java'
apply plugin: 'maven-publish'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'sh', '-c', 'git describe --tags --first-parent || echo "0"'
        standardOutput = stdout
    }
    return stdout.toString().trim().replace("release/", "").replaceAll("/", "_")
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'ch.thus.camel.retry'
archivesBaseName = 'camel-retry'
version = getVersionName()

dependencies {
    def camelVersion = '2.16.1'
    def slfVersion = '1.6.6'
    def log4jVersion = '1.2.17'
    compile(
            "org.apache.camel:camel-core:$camelVersion"
    )

    testCompile(
            "org.apache.camel:camel-test:$camelVersion",
            "org.slf4j:slf4j-log4j12:${slfVersion}",
            "log4j:log4j:${log4jVersion}"
    )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url "$buildDir/repo"
        }
    }
}
